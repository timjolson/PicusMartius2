/*
Nathan Freitag
Steffen Kist
Usman Beg

Program: SPI_Slave
Purpose: This program configures the arduino's serial peripheral
          interface as the slave, and uses the SPI to receive
          a string (a character array) from the other (master)
          arduino serially.  The resulting string is then printed
          to the serial monitor.
 */

void setup()
{
    /* Runs the SPI_SlaveInit function and begins
      serial communication at 9600 bits per second. */
  SPI_SlaveInit();
  Serial.begin(9600);
}

void loop()
{
  /* Creates an empty character array to store the
    received data. */
  char myData[30] = "";
  

  /* Runs a loop to load the characters into the
    character array one character at a time as they
    are received from the other arduino. Each run of
    the loop calls the SPI_SlaveReceive function to
    get the character. */
  for(int i = 0; i < 15; i++)
  {
    myData[i] = SPI_SlaveReceive();
  }
  
  Serial.println(myData);
}


/* SPI_SlaveInit function: sets up the control registers
    of the SPI to set it up as the slave. */
void SPI_SlaveInit(void)
{
  /* Port B Data Direction Register: sets pin B4 as an
      output.  All other port B pins remain inputs. */
  DDRB = (1<<DDB4);


  /* SPI Control Register: sets the SPE bit to 1. The
    SPE bit enables the SPI. */
  SPCR = (1<<SPE);
}


/* SPI_SlaveReceive function: receives the data. */
char SPI_SlaveReceive(void)
{
  /* SPI Status Register: Do nothing until the SPI
      interrupt flag is set.  The flag is set when
      the current data transfer is complete. */
  while(!(SPSR & (1<<SPIF)))
  ;
  
  /* Return Data Register */
  return SPDR;
}